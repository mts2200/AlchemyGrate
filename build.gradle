buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
			changing true
		}
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'idea'

version = "1.1.0"
group = "com.technicianlp.alchgrate"
archivesBaseName = "AlchemicalGrate"

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // you may put jars on which you depend on in ./libs

    // This mod requires Baubles and Thaumcraft in the ./libs folder to compile
//	 implementation name: 'Baubles-1.7.10-1.0.1.10-dev'
//	 implementation name: 'Thaumcraft-1.7.10-4.2.3.5-dev'
}

sourceSets {
	main {
		output.resourcesDir = output.classesDirs.first()
	}
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.register('devJar', Jar) {
    dependsOn reobf
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier 'dev'

    from sourceSets.main.output
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier 'dev-sources'

    from sourceSets.main.allSource
}

artifacts {
	archives devJar
	archives sourcesJar
}
